apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'app.cash.zipline'
apply plugin: 'app.cash.redwood'

kotlin {
  iosArm64()
  iosX64()
  iosSimulatorArm64()

  jvm()

  js {
    browser()
    binaries.executable()
  }

  sourceSets {
    commonMain {
      dependencies {
        api libs.redwood.treehouse
        api libs.redwood.treehouse.lazylayout.compose
        api libs.redwood.treehouse.lazylayout.api
      }
    }

    jsMain {
      dependencies {
        api project(":presenter")
        implementation project(":schema:compose:protocol")
      }
    }

    iosMain {
      dependsOn(commonMain)
    }
    targets.all { target ->
      // Some Kotlin targets do not have this property, but native ones always will.
      if (target.hasProperty('platformType') && target.platformType.name == "native") {
        target.compilations.main.defaultSourceSet.dependsOn(sourceSets.iosMain)
      }
    }
  }
}

zipline {
  mainFunction = 'com.example.redwood.emojisearch.treehouse.preparePresenters'
}
